module(name = "rustls-openssl-fips-example")

bazel_dep(name = "aspect_bazel_lib", version = "2.19.4")
bazel_dep(name = "bazel_skylib", version = "1.8.1")
bazel_dep(name = "container_structure_test", version = "1.19.1")
bazel_dep(name = "platforms", version = "1.0.0")
bazel_dep(name = "rules_cc", version = "0.1.4")
bazel_dep(name = "rules_oci", version = "2.2.6")
bazel_dep(name = "rules_pkg", version = "1.1.0")
bazel_dep(name = "rules_rust", version = "0.62.0")

bazel_dep(name = "toolchains_llvm", version = "1.3.0", dev_dependency = True)

## fetch pre-built copy of openssl

bazel_dep(name = "openssl")
archive_override(
    module_name = "openssl",
    integrity = "sha256-WJuG6a3TkGs8wj8EtXQrwirAelxqTkGh7Euf2Ylvt8w=",
    strip_prefix = "openssl-prebuilt-openssl-prebuilt-1.1.1w",
    urls = ["https://github.com/scasagrande/openssl-prebuilt/archive/refs/tags/openssl-prebuilt-1.1.1w.tar.gz"],
)

## oci

oci = use_extension("@rules_oci//oci:extensions.bzl", "oci")
oci.pull(
    name = "ubi_minimal",
    digest = "sha256:89f2c9794a3357a8463e3acc4dad45075b1a9a63ae05d4a493dd1451625f2222",
    image = "registry.access.redhat.com/ubi8/ubi-minimal",
    platforms = [
        "linux/amd64",
        "linux/arm64",
    ],
)
use_repo(oci, "ubi_minimal", "ubi_minimal_linux_amd64", "ubi_minimal_linux_arm64")

## rust

rust = use_extension("@rules_rust//rust:extensions.bzl", "rust")
rust.toolchain(
    edition = "2021",
    extra_target_triples = [
        "aarch64-unknown-linux-gnu",
        "x86_64-unknown-linux-gnu",
    ],
)
use_repo(rust, "rust_toolchains")

register_toolchains("@rust_toolchains//:all")

crate = use_extension("@rules_rust//crate_universe:extension.bzl", "crate")
crate.from_cargo(
    name = "crates",
    cargo_lockfile = "//third_party:Cargo.lock",
    manifests = ["//third_party:Cargo.toml"],
    supported_platform_triples = [
        "aarch64-apple-darwin",
        "aarch64-unknown-linux-gnu",
        "x86_64-apple-darwin",
        "x86_64-unknown-linux-gnu",
    ],
)
crate.annotation(
    build_script_data = [
        "@openssl//:openssl_lib",
        "@openssl//:openssl_include",
    ],
    build_script_data_glob = ["build/**/*.c"],
    build_script_env = {
        "OPENSSL_NO_VENDOR": "1",
        "OPENSSL_LIB_DIR": "$(execpath @openssl//:openssl_lib)",
        "OPENSSL_INCLUDE_DIR": "$(execpath @openssl//:openssl_include)",
    },
    compile_data = ["@openssl//:openssl_lib"],
    crate = "openssl-sys",
    deps = ["@openssl"],
)
use_repo(crate, "crates")

inject_repo(crate, "openssl")

## cc

llvm = use_extension("@toolchains_llvm//toolchain/extensions:llvm.bzl", "llvm", dev_dependency = True)
llvm.toolchain(
    llvm_version = "18.1.8",
    sha256 = {
        "darwin-aarch64": "d28437d58615ccae434c289bcf5e0d10f031c732d7b60a5090541dbbbd381a01",
        "darwin-x86_64": "f0c103bc2a19ffee7b55df2798188f085756e5668adc7c18d05c7f6705871828",
        "linux-aarch64": "e9695cb9a8d068236b1c902af877be396eb0110d87525db49c68911b571f6976",
        "linux-x86_64": "4d0bbec4acd23006d1353d0cd255144df8c910981f5474f3030bfde3dc75ccfa",
    },
    stdlib = {
        "linux-x86_64": "stdc++",
        "linux-aarch64": "stdc++",
    },
    urls = {
        "darwin-aarch64": ["https://github.com/MaterializeInc/toolchains/releases/download/clang-18.1.8-5/darwin_aarch64.tar.zst"],
        "darwin-x86_64": ["https://github.com/MaterializeInc/toolchains/releases/download/clang-18.1.8-5/darwin_x86_64.tar.zst"],
        "linux-aarch64": ["https://github.com/MaterializeInc/toolchains/releases/download/clang-18.1.8-5/linux_aarch64.tar.zst"],
        "linux-x86_64": ["https://github.com/MaterializeInc/toolchains/releases/download/clang-18.1.8-5/linux_x86_64.tar.zst"],
    },
)
llvm.sysroot(
    name = "llvm_toolchain",
    label = "@sysroot_linux_x86_64_2_28//:sysroot",
    targets = ["linux-x86_64"],
)
llvm.sysroot(
    name = "llvm_toolchain",
    label = "@sysroot_linux_aarch64_2_28//:sysroot",
    targets = ["linux-aarch64"],
)
use_repo(llvm, llvm_toolchain_llvm = "llvm_toolchain")

register_toolchains(
    "@llvm_toolchain_llvm//:all",
    dev_dependency = True,
)

http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

http_archive(
    name = "sysroot_linux_x86_64_2_28",
    build_file_content = """
filegroup(
    name = "sysroot",
    srcs = glob(["**"]),
    visibility = ["//visibility:public"],
)
    """,
    sha256 = "77f5a2e793607139c8ad9607772e7862b1711430a9a1b2926c16128c3355e987",
    url = "https://github.com/scasagrande/toolchains_llvm_sysroot/releases/download/linux-sysroot-4_18-2_28-10_3_0/linux-sysroot-x86_64.tar.zst",
)

http_archive(
    name = "sysroot_linux_aarch64_2_28",
    build_file_content = """
filegroup(
    name = "sysroot",
    srcs = glob(["**"]),
    visibility = ["//visibility:public"],
)
    """,
    sha256 = "8462b3c1bbb7d6e6b5ec48e3b64d59671155b0a522a28e3bcabef67bced3e008",
    url = "https://github.com/scasagrande/toolchains_llvm_sysroot/releases/download/linux-sysroot-4_18-2_28-10_3_0/linux-sysroot-aarch64.tar.zst",
)
